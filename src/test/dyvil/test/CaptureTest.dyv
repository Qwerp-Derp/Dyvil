package dyvil.test

public class CaptureTest
{
	public static void main([String] args)
	{
		lambdaCapture()
		lambdaThisCapture()
		anonClassCapture()
		anonClassThisCapture()
	}
	
	private static void lambdaCapture()
	{
		var abc = -1
		var fun = (int i) => (int j) => (int k) => (i, j, k, abc)
		var res = fun(1)(2)(3)

		Main.test("Lambda Capture", (1, 2, 3, -1), res)
	}
	
	class ThisCapture
	{
		public Object anonymous() = new Object {
			public override String toString() = this[ThisCapture].toString
		}
		
		public -> ThisCapture lambda() = => this

		public override String toString() = System.identityHashCode(this).toHexString
	}
	
	private static void lambdaThisCapture()
	{
		var instance = new ThisCapture
		var lambda = instance.lambda
		var res = lambda()

		Main.test("Lambda This Capture", instance, res)
	}
	
	private static void anonClassCapture()
	{
		var abc = 123
		var obj = new Object {
			public override String toString() = abc.toString
		}
		
		Main.test("Anonymous Class Capture", abc.toString, obj.toString)
	}
	
	private static void anonClassThisCapture()
	{
		var instance = new ThisCapture
		var obj = instance.anonymous

		Main.test("Anonymous Class This Capture", instance, obj, instance.toString == obj.toString)
	}
}
